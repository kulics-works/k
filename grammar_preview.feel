## Grammar Overview

## Export Name Space
mod demo;

## Import Name Space
use {
    System;
    System.Text;
    System.Threading.Tasks;
    System.ComponentModel.DataAnnotations.Schema;
    System.ComponentModel.DataAnnotations;
    . = IO.File; ## 可以隐藏元素使用内容
}

let main() = {
    ## Define, 一般情况下编译器会自动判断类型
    let string: String = "10";
    let number: Float = 1.2;
    let integer: Int = 123;
    let boolean: Bool = true;
    let smallFloat = (1.2).toFloat32(); ## basic obj convert

    ## Const
    let pi = 3.141592653;

    ## Mark String
    let format = "the value is \{integer}, \{number}, \{boolean}";

    ## Array
    let array: [Int] = arrayOf(1, 2, 3);
    let array2: [Int] = [1, 2, 3];

    ## List
    let list: List(Int) = [1, 2, 3, 4, 5];
    print( list[0] ); ## 使用下标获取

    ## Map, 前面为 key，后面为 value
    let map: Map(String, Bool) = [("1", false), ("2", true)];
    print( map["1"] ); ## 使用key获取

    ## Function
    let f(a: Int): Int = a;

    ## Function with no params no return
    let do_something_void(): Void = {
        do_something_a();
        do_something_b();
    }

    ## Full Function with params and return
    let do_something_with_params(x: Int, y: String): (a: Int, b: String) = (x, y);

    ## Function type
    let f: (x: Int, y: Int)->Int = fn(x: Int, y: Int): Int = x + y;
    let f: (Int, Int)->Int = fn(x, y) = x + y;
    let f(x: Int, y: Int): Int = x + y;
    let f(x: Int, y: Int) = x + y;
    let g(): Void = {}
    let g: ()->Void = fn() = {}

    ## Currying
    let add = fn(x: Int) = fn(y: Int) = x + y;
    let add: (Int)->(Int)->Int = fn(x) = fn(y) = x + y;
    let add(x: Int) = fn(y: Int) = x + y;
    let add(x: Int)(y: Int) = x + y;
    let add: (Int)->(Int)->Int = fn(x)(y) = x + y;
    let v = add(1)(2);

    ## Lambda Function
    listOf(1, 2, 3, 4).filter(fn(x) = x % 2 == 0).map(fn(x) = x * x).sum();

    ## Func params
    let f(i: (Int)->Int) = i(1);
    f(fn(x: Int): Int = x);
    f(fn(x) = x);

    do_something_void();
    ## use _ discard return value
    _ = do_something_with_params(3, "test");

    ## if-do, single branch structure
    if a < b do {
        ...
    }

    ## if-let, if with init 
    if let a = 1; a < 0 do {
        ...
    }

    ## if-then-else, double branch structure
    let v = if true then 0 else 0;
    if true then {
        ...
    } else {
        ...
    }

    if a then ...
    else if b then ...
    else ...;

    let op: Option(Int) = Some(1);
    if op as Some(v) do {
        printLine(v)
    }

    ## while-do, single branch structure
    while a < b do {
        ...
    }

    ## while-let, while with init 
    while let mut i = 0; i < 10 do {
        i += 1
    }

    ## while-then-else, double branch structure, break can with value
    let v = while true then break 1 else 2;
    while true then {
        ...
    } else {
        ...
    }

    let iter: Iterator(Int) = listOf(1,2,3).iter();
    while iter.next() as Some(v) do {
        printLine(v)
    }

    ## when
    when {
        a < b then "a",
        b < c then {
            ...
            fallthrough
        },
        c < d then "c",
        else "d",
    }

    when x as {
        1 then "a",
        2 then "b",
        3 then "c",
        _ then "d",
    }

    ## when-let
    when let x = makeInt(); x as {
        1 then "a",
        2 then "b",
        3 then "c",
        _ then "d",
    }

    ## for
    for array as item do {
        ...
    }

    ## for-let
    for let arr = makeArray(); arr as item do {
        ...
    }

    for array as {
        1 then "a",
        2 then "b",
        3 then "c",
        _ then "d",
    }

    ## Loop, use identify to take out single item
    for array as item do printLine(item);
    
    ## take index and value, both worked at Dictionary
    for array.withIndex() as (index, value) do {
        printLine((index, value));
    }
    
    for 0..10 as i do {
        printLine(i);
    }
    
    ## 可以通过 this 来访问包自身属性或方法，当作为指针类型使用时 this 是指针，否则是值类型。 this 可以自定义。
    type Button(width: Int, height: Int, background: String, title: String);
    given Button with {
        this.click(): Void = {
            printLine(title);
            doSomeThingA();
            doSomeThingB();
        }
    }
    ## 私有属性，不能被外部访问
    type Image(_width: Int, _height: Int, _source: String);

    ## Interface
    type Animation = {
        this.move(s: Int): Void;
        this.stop(): Void;
    }

    ## Combine Template
    type ImageButton(as image: Image, as button: Button);
    given ImageButton: Animation with {
        ## Implement Interface
        this.move(s: Int): Void = {
            let t = 5000/s;
            play( s + t );
        }
        this.stop(): Void = {}
    }

    ## Create an Template object
    let btn = Button(1, 2, "Cancel");
    let img = Image(1, 2, "?.jpg");
    let mut ib: ImageButton = Image(img, btn);
    ib = (img, btn);
    ## Calling property
    ib.title = "OK";
    ## Calling method
    ib.show();
    ## Calling protocol
    ib.move(6);

    ## Use Interface
    let play_animation(a: Animation) = {
        a.move(1000);
        a.stop();
    }
    play_animation(ib);

    ## Annotation
    #table("user") #property(false, name = "d", hide = true)
    type mut User(
        #column("id") #required #key
        id: String,
        #column("nick_name") #required
        nick_name: String,
        #column("time_update") #required
        time_update: Int,
    );
    
    ## Generic Type
    type mut Table(T)(data: T);
    given(T) Table(T) with {
        mut this.setData(d: T): Void = {
            this.data = d;
        }
    }
    Table(Int)(1);
    ## Generic function
    let add(T: Addable)(x1: T, x2: T): T = x1 + x2;
    add(Int)(1, 2);

    type Void();

    type Color = { Red(); Green(); Blue() }

    type Point(x: Int, y: Int);

    type Pair(T1, T2)(first: T1, second: T2);

    type Option(T) = { Some(value: T); None() }

    let opt() = {
        let mut a: Option(Int) = Some(1);
        a = None();
        when a as {
            Some(v) then printLine(v),
            None then printLine("none"),
        }

        let b: Option(Option(Int)) = Some(Some(2));
        when b as {
            Some(Some(v)) then printLine(v),
            Some(None) then printLine("some(none)"),
            None then printLine("none"),
        }
    }

    type mut MyList(T)(arr: [T]);
    ## operator override
    given(T) MyList(T): Addable(T) with {
        ## a + e
        mut this.plus(element: T): Void = {
            this.arr = this.arr.append(element);
        }
    }
    given(T) MyList(T): MutIndexable(T) with {
        ## e = a[i]
        this.get(index: Int): T = this.arr[index];
        ## a[i] = e
        mut this.set(index: Int, element: T): Void = this.arr[index] = element;
    }
    
    ## Iterator pattern
    type mut Iterator(T) = {
        mut this.next(): Option(T);
    }

    type Iterable(T) = {
        this.iter(): mut Iterator(T);
    }

    type ListNode(T) = {
        Empty();
        Node(head: T, tail: ListNode(T));
    } with {
        this.iter(): mut Iterator(T) = mut ListNodeIterator(this);
    }

    type mut ListIterator(T)(li: ListNode(T));

    given(T) ListIterator(T) with
        mut this.next(): Option(T) =
            if this.li as Node(h, t) then {
                this.li = t;
                Some(h)
            } else {
                None()
            }

    type TreeNode = { Empty(); Node(val: Int, left: TreeNode, right: TreeNode) }

    type Nat = { Zero(); Suc(n: Nat) }
}

type Foo(value: Int);

type Color = { Red(); Green(); Blue() }

type Color(rgb: String) = {
    Red("ff0000");
    Green("00ff00");
    Blue("0000ff");
}

type Planet(mass: Float, radius: Float) = {
    Mercury(3.303e+23, 2.4397e6);
    Venus(4.869e+24, 6.0518e6);
    Earth(5.976e+24, 6.37814e6);
    Mars(6.421e+23, 3.3972e6);
    Jupiter(1.9e+27, 7.1492e7);
    Saturn(5.688e+26, 6.0268e7);
    Uranus(8.686e+25, 2.5559e7);
    Neptune(1.024e+26, 2.4746e7);
}

type Option(T) = { Some(value: T); None() }

type Expr(T) = {
    IntExpr(value: Int): Expr(Int);
    BoolExpr(value: Bool): Expr(Bool);
    EqualExpr(left: Expr(T), right: Expr(T)): Expr(Bool);
    AddExpr(left: Expr(Int), right: Expr(Int)): Expr(Int);
}

type UserName(name: String);
type Password(hash: Hash);
type Identify = UserName | Password;
type HashKey(T) = Equatable(T) & Hashable;

given(T: ToString) Option(T) with {
    this.toString(): String = 
        if this is Some(v)
            then "Some(\{v})" 
            else "None()";
}

given Foo: ToString with {
    this.toString(): String = "foo";
}
given(T, R) (T)->R with {
    this.toAny(): Any = this;
}

type LinkedList(T) = {
    Cons(head: T, tail: LinkedList(T));
    Empty();
}

given(T) LinkedList(T) with {
    new(): This = Empty();
}

type mut List(T) = {
    this.get(index: Int): T;

    mut this.set(index: Int, element: T): Void;
}

type mut ArrayList(T) = { ArrayList(_arr: Array(T), size: Int) }
type mut ArrayList(T)(_arr: Array(T), size: Int);

given(T) ArrayList(T): List(T) with {
    new(): mut This = mut This([], 0);

    this.get(index: Int): T = this._arr.get(index);

    mut this.set(index: Int, element: T): Void = this._arr.set(index, element);

    this.map(R)(f: (T)->R): Iterator(R) = MapIter(T, R)(this, f);
}

given ArrayList(Int) with {
    this.sum(): Int = reduce(this.iterator(), 0, fn(l, r) = l + r);
}

type ToString = {
    this.toString(): String;
}

given Int: ToString with {
    this.toString(): String = cprintf(this);
}

given(T: ToString) ArrayList(T): ToString with {
    this.toString(): String = join_String(this.iterator());
}

given Int with {
    default(): This = 0;
}

given String with {
    default(): This = "";
}

given(T) Array(T) with {
    default(): This = arrayOf(T)();
}

type ToString = {
    this.toString(): String;
}

type mut InputStream = {
    mut this.read(to: Array(Byte)): Int;
}

type mut OutputStream = {
    mut this.write(from: mut Array(Byte)): Int;
    mut this.flush(): Void;
}

type mut Array(T)(_source: RawArray(T), _start: Int, _len: Int) with {
    create(size: Int, item: T) = mut Array(T)(RawArray.new(size, item), 0, size);

    this.size() = this._len;

    this.get(index: Int) = this._source[index + _start];
    
    mut this.set(index: Int, value: T) = this._source[index + _start] = value;
    
    this.slice(start: Int, len: Int): Array(T) = 
        Array(T)(this._source, start, len);

    mut this.sliceMut(start: Int, len: Int): mut Array(T) = 
        mut Array(T)(this._source, start, len);
}

type mut ArrayIterator(T)(_source: Array(T), index: Int): Iterator(T) with {
    of(source: Array(T)) = ArrayIterator(source, -1);
    
    mut this.next(): Option(T) = 
        if this.index < this._source.size() then {
            this.index += 1;
            Some(this._source[this.index])
        } else {
            None()
        }
}

type String(_data: Array(Char)) with {
    of(data: Array(Char)): String = String(data.clone());

    this.toString(): String = String(this._data.clone());

    this.clone(): String = String(this._data.clone());

    this.iter(): mut StringIterator = StringIterator.of(this);
}

type mut StringIterator(_source: String, index: Int): Iterator(Char) with {
    of(source: String) = mut StringIterator(source, -1);
    
    mut this.next(): Option(Char) = 
        if this.index < this._source._data.size() then {
            this.index += 1;
            Some(this._source[this.index])
        } else {
            None()
        }
}

type mut Node(T)(value: T, next: Option(Node(T)));

type mut LinkedList(T)(head: Option(Node(T)), size: Int) with {
    mut this.prepend(v: T): Void = {
        let node = mut Node(v, None());
        if this.head as None then {
            this.head = node;
        } else {
            node.next = this.head;
            this.head = node;
        }
        this.size += 1;
    }
}

#*
1. 数据是事物本身既有的
1. 事物的行为是由事物的数据决定的
1. 事物的行为可以根据所在环境的不同而有差异
1. 分类是对事物的抽象，通常形成于事物出现之后。
1. 分类描述某些特征，以此区分不同的事物。

1. 使用实例成员函数重载操作符
1. 默认数字类型是平台相关类型
1. 没有空指针
1. 没有继承
1. 没有函数重载
1. 没有字面量重载
1. 没有集合字面量
1. 没有隐式转换
1. 泛型
1. 模式匹配
1. 变长参数
1. 没有构造函数
1. 类型上区分可变和不可变
*#
